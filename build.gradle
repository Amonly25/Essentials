import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.ajoberstar.grgit' version '4.1.0'
}

allprojects {
    group = 'net.ess3'
    version = '2.18.1'
}

def commitsSinceLastTag() {
    def tags = grgit.tag.list().stream().map({it.commit}).toList()
    def commit = grgit.head()
    def depth = 0
    while (true) {
        if (tags.contains(commit))
            return depth
        depth++
        commit = grgit.resolve.toCommit(commit.parentIds.get(0))
    }
}

ext {
    GIT_COMMIT = grgit.head().abbreviatedId
    GIT_DEPTH = commitsSinceLastTag()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://ci.ender.zone/plugin/repository/everything/' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url = 'https://papermc.io/repo/repository/maven-public/' }
        maven { url = 'https://jitpack.io' }
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.mockito:mockito-core:3.2.0'

        if (project.name != "1_8Provider" && project.name != "PaperProvider" && project.name != "NMSReflectionProvider") { // These providers use their own bukkit versions
            compileOnly 'org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT'
            testCompileOnly 'org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT'
        }
    }

    processResources {
        filter(ReplaceTokens, beginToken: '${',
                endToken: '}', tokens: ["full.version": "${version}.${GIT_DEPTH}".toString()])
    }

    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        }
    }

    sourceCompatibility = '1.8'
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Jar) {
        archiveVersion.set("${this.version}.${GIT_DEPTH}".toString())
    }
}

def outputTasks() {
    [":EssentialsX:shadowJar", ":EssentialsXAntiBuild:jar", ":EssentialsXChat:jar",
     ":EssentialsXGeoIP:shadowJar", ":EssentialsXProtect:jar", ":EssentialsXSpawn:shadowJar",
     ":EssentialsXXMPP:shadowJar"].stream().map({ tasks.findByPath(it) })
}

task copyToJars(type: Copy) {
    from(project(":EssentialsX").sourceSets.main.output.resourcesDir) {
        exclude 'plugin.yml'
    }

    outputTasks().forEach {
        println it
        from(it.outputs.files)
    }

    rename '(.*)-all.jar', '$1.jar'

    into file('jars')
}

copyToJars.dependsOn tasks.findByPath(":EssentialsX:processResources")
outputTasks().forEach { copyToJars.dependsOn it }

task build() {}
build.dependsOn copyToJars

task clean() {
    delete file('jars')
}
